dnl
dnl Process this file with autoconf to produce a configure script
dnl

AC_PREREQ(2.13)
AC_INIT(build/rules.mk.in)

AC_CONFIG_HEADER(include/apu_config.h)
AC_CONFIG_AUX_DIR(build)

AC_PROG_CC
AC_CHECK_PROG(RM, rm, rm)

dnl
dnl figure out where APR is located
dnl
AC_MSG_CHECKING(for APR)
AC_ARG_WITH(apr,
[  --with-apr=DIR         path to APR source or the APR includes.],
[
    if test "$withval" = "yes"; then
        AC_MSG_ERROR(You need to specify a directory with --with-apr)
    fi
    absdir="`cd $withval ; pwd`"
    if test -f "$absdir/apr_pools.h"; then
	APR_INCLUDES="$absdir"
    elif test -f "$absdir/include/apr_pools.h"; then
	APR_INCLUDES="$absdir/include"
    fi
],[
    dnl see if we can find APR
    if test -f "$srcdir/apr/include/apr_pools.h"; then
	APR_INCLUDES="$srcdir/apr/include"
    fi
])
if test -z "$APR_INCLUDES"; then
    AC_MSG_RESULT(not found)
    AC_MSG_ERROR(APR could not be located. Please use the --with-apr option.)
fi
AC_MSG_RESULT($APR_INCLUDES)


dnl
dnl compute the top directory of the build
dnl
top_builddir="`cd $srcdir ; pwd`"
AC_SUBST(top_builddir)

dnl
dnl prep libtool
dnl
echo "performing libtool configuration..."
AC_PROG_LIBTOOL

dnl
dnl set up the compilation flags and stuff
dnl
INCLUDES="-I$APR_INCLUDES -I\$(top_builddir)/include"

CFLAGS="$CFLAGS $INCLUDES"


AC_OUTPUT([
	Makefile
	build/Makefile
	build/rules.mk
	src/Makefile
	src/buckets/Makefile
	src/crypto/Makefile
	src/dbm/Makefile
	src/dbm/sdbm/Makefile
	src/encoding/Makefile
	src/hooks/Makefile
	src/uri/Makefile
	src/xml/Makefile
	test/Makefile
	])
